RPI_REMOTE_TOOLS_PATH := "rpi_remote_tools"
RPI_REMOTE_TOOLS_MODULE := "rpi_remote"

# Executing "just" without arguments is listing all recipes
list-recipes:
    @just --list --unsorted

# Checking about application is already running on Raspberry Pi device.
check configurations="{}":
    @just _rpi --rpi-check-app '{{configurations}}'

# Killing running application on Raspberry Pi device
kill configurations="{}":
    @just _rpi --rpi-kill-app '{{configurations}}'

# Restarting application service on Raspberry Pi device
start configurations="{}":
    @just _rpi --rpi-restart-service '{{configurations}}'

# Starting application on Raspberry Pi device (first it will kill already running app) 
run configurations="{}":
    @just _rpi --rpi-run-app '{{configurations}}'

# Copying application to Raspberry Pi device and then starting application
sync configurations="{}":
    @just _rpi --rpi-copy-code '{{configurations}}'

# Live stream from Raspberry Pi device tmux session
tmux configurations="{}":
    @just _rpi --rpi-tmux '{{configurations}}'

_rpi command configurations:
    @uv run --quiet --project {{RPI_REMOTE_TOOLS_PATH}} \
        python -m {{RPI_REMOTE_TOOLS_PATH}}.{{RPI_REMOTE_TOOLS_MODULE}} {{command}} \
        --configurations '{{configurations}}'

